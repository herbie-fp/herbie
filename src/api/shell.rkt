#lang racket

(require "../syntax/read.rkt"
         "../utils/common.rkt"
         "sandbox.rkt"
         "server.rkt")
(provide run-shell)

(define (get-input)
  (printf "herbie> ")
  (with-handlers ([(or/c exn:fail:user? exn:fail:read?) (λ (e)
                                                          ((error-display-handler) (exn-message e) e)
                                                          (get-input))])
    (define input
      (parameterize ([read-decimal-as-inexact false])
        (read-syntax "stdin" (current-input-port))))
    (cond
      [(eof-object? input)
       (printf "\n")
       eof]
      [else (parse-test input)])))

(define (run-shell)
  (define seed (get-seed))
  (eprintf "Herbie ~a with seed ~a\n" *herbie-version* seed)
  (eprintf "Find help on https://herbie.uwplse.org/, exit with ~a\n"
           (match (system-type 'os)
             ['windows "Ctrl-Z Enter"]
             [_ "Ctrl-D"]))
  (with-handlers ([exn:break? (λ (e) (exit 0))])
    (for ([test (in-producer get-input eof-object?)]
          [idx (in-naturals)])
      (define result (wait-for-job (start-job 'improve test #:seed seed)))
      (define status (hash-ref result 'status))
      (define time (hash-ref result 'time))
      (define table-data (get-table-data-from-hash result ""))
      (match status
        ['success (pretty-print (unparse-result table-data) (current-output-port) 1)]
        ['failure
         (match-define (list 'exn type msg url locs traceback) (hash-ref result 'backend))
         (printf "; ~a\n" msg)
         (for ([loc (in-list locs)])
           (match-define (list msg file line col pos) loc)
           (printf ";   ~a:~a~a: ~a\n" file line col msg))
         (printf "; See <https://herbie.uwplse.org/doc/~a/~a> for more.\n"
                 *herbie-version* url)]
        ['timeout (printf "Timeout in ~as (see --timeout option)\n" (/ time 1000))]
        [else (error 'run-shell "unknown result type ~a" status)]))))
